plugins {
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.jfrog.artifactory' version "3.1.0"
    id 'com.jfrog.bintray' version "1.2"
}

group = 'com.moowork.gradle'

ext {
    bintrayUser = System.env.'BINTRAY_USER'
    bintrayKey = System.env.'BINTRAY_KEY'
    isMaster = System.env.'TRAVIS_BRANCH' == "master"
    isCI = System.env.'CI'
    compatibilityVersion = 1.6
}

sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile( 'org.spockframework:spock-core:0.7-groovy-2.0' ) {
        exclude module: 'groovy-all'
    }
    compile 'com.moowork.gradle:gradle-node-plugin:0.10'
}

task sourcesJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar( type: Jar ) {
    classifier 'javadoc'
    from groovydoc
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

task ci {
    dependsOn 'build' // , 'artifactoryPublish'
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
        }
    }
}

artifactoryPublish {
    onlyIf { isMaster }
    mustRunAfter 'build'
    publications 'mavenJava'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'

    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
            maven = true
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publications = ['mavenJava']
    publish = true

    pkg {
        repo = 'maven'
        name = 'gradle-grunt-plugin'
        licenses = ['Apache-2.0']
        desc = 'Gradle plugin for executing grunt builds. '
        websiteUrl = 'https://github.com/srs/gradle-grunt-plugin'
        issueTrackerUrl = 'https://github.com/srs/gradle-grunt-plugin/issues'
        vcsUrl = 'https://github.com/srs/gradle-grunt-plugin.git'
        labels = ['java', 'gradle', 'groovy', 'grunt', 'node']
        publicDownloadNumbers = true
        version {
            attributes = ['gradle-plugin': 'com.moowork.grunt:com.moowork.gradle:gradle-grunt-plugin']
        }
    }
}
